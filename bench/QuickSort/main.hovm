// Generates a random list
(Randoms s 0) = (Nil)
(Randoms s l) = (Cons s (Randoms (+ (* s 1664525) 1013904223) (- l 1)))

// Sums all elements in a concatenation tree
(Sum Empty)        = 0
(Sum (Single a))   = a
(Sum (Concat a b)) = (+ (Sum a) (Sum b))

// Parallel QuickSort
(Quicksort Nil)                 = Empty
(Quicksort (Cons x xs))         = (Quicksort_ x xs)
(Quicksort_ p Nil)              = (Single p)
(Quicksort_ p (Cons x xs))      = (Split p (Cons p (Cons x xs)) Nil Nil)
  (Split p Nil         min max) = (Concat (Quicksort min) (Quicksort max))
  (Split p (Cons x xs) min max) = (Place p (< p x) x xs min max)
  (Place p 0 x xs      min max) = (Split p xs (Cons x min) max)
  (Place p 1 x xs      min max) = (Split p xs min (Cons x max))

(Main n) = (Sum (Quicksort (Randoms 1 10000000)))

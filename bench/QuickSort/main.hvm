// Generates a random list
(Randoms s 0) = (Nil)
(Randoms s l) = (Cons s (Randoms (+ (* s 1664525) 1013904223) (- l 1)))

// Sums all elements in a concatenation tree
(Sum Empty)        = 0
(Sum (Single a))   = a
(Sum (Concat a b)) = (+ (Sum a) (Sum b))

// Parallel QuickSort
// TODO: the syntax on the README isn't implemented
//       yet, so we need a 4th clause. Sorry :P
(Sort Nil)         = Empty
(Sort (Cons x xs)) = (Sort_ x xs)
(Sort_ p Nil)      = (Single p)
(Sort_ p xs)       = (Split p (Cons p xs) Nil Nil)

// Splits list in two partitions
(Split p Nil         min max) = (Concat (Sort min) (Sort max))
(Split p (Cons x xs) min max) = (Place p (< p x) x xs min max)

// Moves element to its partition
(Place p 0 x xs min max) = (Split p xs (Cons x min) max)
(Place p 1 x xs min max) = (Split p xs min (Cons x max))

// Sorts and sums n random numbers
(Main n) = (Sum (Sort (Randoms 1 (* 100000 n))))

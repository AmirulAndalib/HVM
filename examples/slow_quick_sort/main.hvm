// Quick Sort
(Sort Nil) = Nil
(Sort (Cons x xs)) =
  let min = (Sort (Filter λn(< n x) xs))
  let max = (Sort (Filter λn(> n x) xs))
  (Concat min (Cons x max))

// Filters a list
(Filter f Nil)         = Nil
(Filter f (Cons x xs)) = (Filter.test (f x) f x xs)
  (Filter.test 0 f x xs) = (Filter f xs)
  (Filter.test 1 f x xs) = (Cons x (Filter f xs))

// Concatenates two lists
(Concat Nil         ys) = ys
(Concat (Cons x xs) ys) = (Cons x (Concat xs ys))

// Generates a random list
(Rnd 0 s) = (Nil)
(Rnd n s) = (Cons s (Rnd (- n 1) (% (+ (* s 1664525) 1013904223) 4294967296)))

// Sums all elements in a concatenation tree
(Sum Nil)         = 0
(Sum (Cons x xs)) = (+ x (Sum xs))

// Sorts and sums n random numbers
(Main n) = (Sum (Sort (Rnd 100000 1)))
